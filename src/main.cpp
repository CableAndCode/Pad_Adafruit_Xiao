#include <Arduino.h>
#include <Adafruit_seesaw.h>
#include <esp_now.h>
#include <WiFi.h>
#include "parameters.h"
#include "messages.h"
#include "mac_adresses.h"
#include "errors.h"
#include <SPI.h>
#include <joystick_read.h>
#include <TFT_eSPI.h>
#include <DisplayManager.h>


// ----- Konfiguracja sprzƒôtowa -----

//TFT Display
DisplayManager display;


// Adresy I2C dla gamepad√≥w
constexpr uint8_t GAMEPAD1_ADDR = 0x50;
constexpr uint8_t GAMEPAD2_ADDR = 0x51;

// Mapowanie przycisk√≥w
constexpr uint8_t BUTTON_X      = 6;
constexpr uint8_t BUTTON_Y      = 2;
constexpr uint8_t BUTTON_A      = 5;
constexpr uint8_t BUTTON_B      = 1;
constexpr uint8_t BUTTON_SELECT = 0;
constexpr uint8_t BUTTON_START  = 16;
const uint32_t button_mask = (1UL << BUTTON_X) | (1UL << BUTTON_Y) | (1UL << BUTTON_START) |
                             (1UL << BUTTON_A) | (1UL << BUTTON_B) | (1UL << BUTTON_SELECT);
const uint32_t button_mask2 = button_mask;



// ----- Globalne zmienne i obiekty -----

//zmienne do przechowywania warto≈õci dryftu joystick√≥w


// Obiekty do obs≈Çugi gamepad√≥w
Adafruit_seesaw ss1, ss2;

volatile int offsetL_X = 0;
volatile int offsetL_Y = 0;
volatile int offsetR_X = 0;
volatile int offsetR_Y = 0;
 

JoystickReader joystickReaderL(offsetL_X, offsetL_Y, true, true);
JoystickReader joystickReaderR(offsetR_X, offsetR_Y, false, false);


// Globalna struktura wiadomo≈õci z pada (heartbeat w polu timestamp)
Message_from_Pad message;

// Mutex do ochrony globalnej struktury (odczyt/zapis)
SemaphoreHandle_t messageMutex = NULL;

// Statystyki transmisji (opcjonalnie) i czasy
volatile uint32_t totalMessages = 0;
volatile uint32_t failedMessages = 0;
volatile uint32_t lastFailedCount = 0;
volatile uint32_t failedPerSecond = 0;
volatile int consecutiveFailures = 0;       // Licznik niepowodze≈Ñ esp-now
volatile int espNowStatus = 0;              // 0 = OK, 1 = WARNING, 2 = ERROR

//zmienne do sprawdzania heartbeatu osobne dla kadego peera (monitora i platformy), w przysz≈Ço≈õci kolejne peery
volatile TickType_t lastHeartbeatTimeMonitor =0;
volatile TickType_t lastHeartbeatTimePlatform =0;    
  


// Konfiguracja ESP-NOW
esp_now_peer_info_t peerInfo;

// ----- Callback wysy≈Çania -----
void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status) {
    totalMessages++;
    if (status == ESP_NOW_SEND_SUCCESS) {
        failedMessages = 0;
    } else {
        failedMessages++;
    }
}

// ----- TASK 1: Odczyt danych gamepad√≥w (TaskGamepads) -----
// Co 25 ms odczytuje dane z gamepad√≥w, normalizuje je i zapisuje do globalnej struktury.
// Ochrona danych za pomocƒÖ mutexu.
void TaskGamepads(void *pvParameters) {
    (void)pvParameters;
    const TickType_t xFrequency = pdMS_TO_TICKS(25);  // Odczyt 40 razy na sekundƒô
    TickType_t xLastWakeTime = xTaskGetTickCount();

    while (1) {
        // Odczyt danych i normalizacja poza sekcjƒÖ krytycznƒÖ
        //unsigned long localTimestamp = millis();            // Odczyt czasu uzyty do heartbeatu, stara wersja
        
        // Odczyt surowych warto≈õci joystick√≥w
        int localL_Joystick_raw_x = ss1.analogRead(14);
        int localL_Joystick_raw_y = ss1.analogRead(15);
        int localR_Joystick_raw_x = ss2.analogRead(14);
        int localR_Joystick_raw_y = ss2.analogRead(15);

        //odczyt warto≈õci joystick√≥w z uwzglƒôdnieniem dryftu
        int localL_Joystick_x = joystickReaderL.getCorrectedValueX(localL_Joystick_raw_x); 
        int localL_Joystick_y = joystickReaderL.getCorrectedValueY(localL_Joystick_raw_y);
        int localR_Joystick_x = joystickReaderR.getCorrectedValueX(localR_Joystick_raw_x);
        int localR_Joystick_y = joystickReaderR.getCorrectedValueY(localR_Joystick_raw_y);

        // Odczyt stan√≥w przycisk√≥w
        int localL_Joystick_buttons_message = ss1.digitalReadBulk(button_mask);
        int localR_Joystick_buttons_message = ss2.digitalReadBulk(button_mask2);

        // Kr√≥tka sekcja krytyczna ‚Äì kopiowanie lokalnych danych do globalnej struktury
        xSemaphoreTake(messageMutex, portMAX_DELAY);
        //message.timestamp = localTimestamp;
        
        message.L_Joystick_raw_x = localL_Joystick_raw_x;
        message.L_Joystick_raw_y = localL_Joystick_raw_y;
        message.R_Joystick_raw_x = localR_Joystick_raw_x;
        message.R_Joystick_raw_y = localR_Joystick_raw_y;
        
        message.L_Joystick_x_message = localL_Joystick_x;
        message.L_Joystick_y_message = localL_Joystick_y;
        message.R_Joystick_x_message = localR_Joystick_x;
        message.R_Joystick_y_message = localR_Joystick_y;
        
        message.L_Joystick_buttons_message = localL_Joystick_buttons_message;
        message.R_Joystick_buttons_message = localR_Joystick_buttons_message;
        xSemaphoreGive(messageMutex);

        // Przyk≈Çadowe monitorowanie zu≈ºycia zasobow ‚Äì mo≈ºna odkomentowaƒá
        /*
        UBaseType_t freeStack = uxTaskGetStackHighWaterMark(NULL); // Dostƒôpna pamiƒôƒá stosu dla tasku
        Serial.print("Wolna pamiƒôƒá stosu: ");
        Serial.println(freeStack);

        Serial.print("Free heap: ");                 // Dostƒôpna pamiƒôƒá heap (RAM)
        Serial.println(esp_get_free_heap_size());

        Serial.print("Free PSRAM: ");                // Dostƒôpna pamiƒôƒá PSRAM (dodatkowa pamiƒôƒá RAM)
        Serial.println(ESP.getFreePsram());

        uint32_t highWaterMark = uxTaskGetStackHighWaterMark(NULL); //ObciƒÖzenie CPU
        Serial.print("CPU Load: ");
        Serial.println(highWaterMark);
        */
        // Odczekanie do nastƒôpnego cyklu
        vTaskDelayUntil(&xLastWakeTime, xFrequency);
    }
}

// ----- TASK 2: Wysy≈Çanie danych ESP-NOW (TaskESPNow) -----
// Co 40 ms odczytuje dane (z mutexem) i wysy≈Ça je przez ESP-NOW.
void TaskESPNow(void *pvParameters) {
    (void)pvParameters;
    const TickType_t xFrequency = pdMS_TO_TICKS(40); // Wysy≈Çka 25 razy na sekundƒô
    TickType_t xLastWakeTime = xTaskGetTickCount();
    Message_from_Pad localMsg;
    
    while (1) {

        xSemaphoreTake(messageMutex, portMAX_DELAY);
        memcpy(&localMsg, &message, sizeof(Message_from_Pad));
        xSemaphoreGive(messageMutex);

        // Wys≈Çanie danych przez ESP-NOW do monitora debug
        esp_err_t result = esp_now_send(macMonitorDebug, (uint8_t *)&localMsg, sizeof(Message_from_Pad));
        // Debug mo≈ºna odkomentowaƒá:
        // if (result == ESP_OK) Serial.println("üì° Dane wys≈Çane");
        // else Serial.println("‚ùå B≈ÇƒÖd wysy≈Çania ESP-NOW");

        // Wys≈Çanie danych przez ESP-NOW do platformy mecanum
        result = esp_now_send(macPlatformMecanum, (uint8_t *)&localMsg, sizeof(Message_from_Pad));
        // Debug mo≈ºna odkomentowaƒá:
        // if (result == ESP_OK) Serial.println("üì° Dane wys≈Çane");
        // else Serial.println("‚ùå B≈ÇƒÖd wysy≈Çania ESP-NOW");

        vTaskDelayUntil(&xLastWakeTime, xFrequency);
    }
}


// ----- TASK 3: Wy≈õwietlanie danych na wyswietlaczu TFT -----
// Co 40 ms aktualizuje wy≈õwietlacz TFT z informacjami o statystykach ESP-NOW.
void TaskTFTScreen(void *pvParameters) {
    (void)pvParameters;
    const TickType_t xFrequency = pdMS_TO_TICKS(40); // Aktualizacja co 40 ms
    TickType_t xLastWakeTime = xTaskGetTickCount();
    TickType_t lastTimestamp = xTaskGetTickCount();

    while (1) {
        vTaskDelayUntil(&xLastWakeTime, xFrequency);

        // Odczytaj warto≈õci joystick√≥w
        xSemaphoreTake(messageMutex, portMAX_DELAY);
        int lx = message.L_Joystick_x_message;
        int ly = message.L_Joystick_y_message;
        int rx = message.R_Joystick_x_message;
        int ry = message.R_Joystick_y_message;
        xSemaphoreGive(messageMutex);

        // Aktualizacja wy≈õwietlacza
        display.updateJoystick(lx, ly, rx, ry);
        display.updateStatus(totalMessages, failedMessages);

        // Wy≈õwietl komunikat
        if (failedMessages == 0) {
            display.showMessage("ESP-NOW OK");
        } else {
            display.showMessage("ESP-NOW ErrrrRROR");
        }
    }
}

//-----------------Task 4: Sprawdzanie heartbeatu-----------//
// Celem jest sprawdzenie, czy dane z gamepad√≥w sƒÖ nadal przesy≈Çane do platformy mecanum oraz do monitora debug
// Je≈õli nie, celem jest wy≈õwietlenie tej wiadomoci na wy≈õwietlaczu TFT w tasku TaskTFTScreen
// zmiana nazewnictwa tasku vTaskESPNowStats na TaskTFTScreen
void TaskCheckHeartbeat(void *pvParameters) {
    (void)pvParameters;
    const TickType_t xFrequency = pdMS_TO_TICKS(50); // Sprawdzanie co 50 ms
    TickType_t xLastWakeTime = xTaskGetTickCount();

    while (1) {
        volatile TickType_t currentTime = xTaskGetTickCount();

        // Sprawd≈∫, czy r√≥≈ºnica w czasie przekroczy≈Ça limit
        if ((currentTime - lastHeartbeatTimeMonitor) > pdMS_TO_TICKS(500)) {
            Serial.println("‚ö†Ô∏è Brak heartbeatu Monitora!");
            // Mo≈ºesz dodaƒá np. restart ESP-NOW, zmianƒô kana≈Çu itd.
        }
        vTaskDelayUntil(&xLastWakeTime, xFrequency);
    }
}



void setup() {
    Serial.begin(115200);
    Wire.begin(5, 6);  // Konfiguracja I2C ‚Äì SDA, SCL
    display.begin();

    

    // Inicjalizacja gamepad√≥w
    if (!ss1.begin(GAMEPAD1_ADDR) || !ss2.begin(GAMEPAD2_ADDR)) {
        Serial.println("‚ùå Gamepad not found!");
        while (1) delay(100);
    }
    Serial.println("‚úÖ Gamepad OK!");

    // Konfiguracja wej≈õƒá przycisk√≥w
    ss1.pinModeBulk(button_mask, INPUT_PULLUP);
    ss1.setGPIOInterrupts(button_mask, 1);
    ss2.pinModeBulk(button_mask2, INPUT_PULLUP);
    ss2.setGPIOInterrupts(button_mask2, 1);
        #if defined(IRQ_PIN)
            pinMode(IRQ_PIN, INPUT);
        #endif
    
    //inicjalizacja joystick√≥w, pobranie warto≈õci poczƒÖtkowych offsetu
    offsetL_X = ss1.analogRead(14);
    offsetL_Y = ss1.analogRead(15);
    offsetR_X = ss2.analogRead(14);
    offsetR_Y = ss2.analogRead(15);
    joystickReaderL.setOffset(offsetL_X, offsetL_Y);
    joystickReaderR.setOffset(offsetR_X, offsetR_Y);
    


    // Konfiguracja ESP-NOW
    WiFi.mode(WIFI_STA);
    WiFi.disconnect();
    if (esp_now_init() != ESP_OK) {
        Serial.println("‚ùå ESP-NOW Init Failed");
        return;
    }
    esp_now_register_send_cb(OnDataSent);


    // Dodanie odbiorcy monitora debug
    memcpy(peerInfo.peer_addr, macMonitorDebug, 6);
    peerInfo.channel = 0;
    peerInfo.encrypt = false;
    if (esp_now_add_peer(&peerInfo) != ESP_OK) {
        Serial.println("‚ùå Failed to add peer");
        return;
    }

    // Dodanie odbiorcy platformy mecanum
    memcpy(peerInfo.peer_addr, macPlatformMecanum, 6);
    if (esp_now_add_peer(&peerInfo) != ESP_OK) {
        Serial.println("‚ùå Failed to add peer");
        return;
    }


    // Utworzenie mutexu do ochrony globalnej struktury danych
    messageMutex = xSemaphoreCreateMutex();
    if (messageMutex == NULL) {
        Serial.println("‚ùå B≈ÇƒÖd tworzenia mutexu!");
        while (1) delay(100);
    }

    // Tworzenie task√≥w FreeRTOS
    xTaskCreate(TaskGamepads, "Gamepads", 4096, NULL, 1, NULL);                 //task do odczytu joystick√≥w
    xTaskCreate(TaskESPNow, "ESPNowSend", 4096, NULL, 1, NULL);                 //task do wysy≈Çania danych przez ESP-NOW
    xTaskCreate(TaskTFTScreen, "TFTScreen", 4096, NULL, 1, NULL);               //task do statystyk
    xTaskCreate(TaskCheckHeartbeat, "Heartbeat", 4096, NULL, 1, NULL);          //task do sprawdzania heartbeatu
}

void loop() {
    // Pusta pƒôtla ‚Äì wszystkie operacje dzia≈ÇajƒÖ w taskach FreeRTOS
}
